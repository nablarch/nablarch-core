group = 'com.nablarch.framework'
version = '1.3.0'
description = 'FW自体の基盤(ハンドラなど)およびコア機能(ログ、リポジトリなど）'

buildscript {
  repositories {
    mavenLocal()
    maven { url "${developLibUrl}" }
    jcenter()
  }
  dependencies {
    classpath "com.nablarch.dev:nablarch-gradle-plugin:${nablarchGradlePluginVersion}"
    classpath "net.saliman:gradle-cobertura-plugin:${coberturaPluginVersion}"
  }
}

apply plugin: 'com.nablarch.dev.nablarch-build'
apply plugin: 'com.nablarch.dev.nablarch-maven-deploy'
apply plugin: 'com.nablarch.dev.nablarch-sonarqube'
apply plugin: 'com.nablarch.dev.nablarch-version'
apply plugin: 'cobertura'

// ビルド時のJavaバージョンを指定する
sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

configurations {
  all*.exclude module: 'nablarch-test-support'
  all*.exclude module: 'h2'
  jmockit
}

dependencies {
  jmockit 'org.jmockit:jmockit:1.21'
  testCompile configurations.jmockit.dependencies
  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-all:1.3'
  // Cobertura
  cobertura ("net.sourceforge.cobertura:cobertura:1.9.4.1")

  testRuntime('com.nablarch.integration:nablarch-slf4j-adaptor:1.0.0') {
    exclude module: 'nablarch-core'
  }
  testRuntime 'org.slf4j:slf4j-simple:1.7.24'
}

// カバレッジを取るための設定
File nabDir = new File(buildDir, 'nablarch')
File nabClassesDir = new File("${buildDir}/classes/main/")
File nabSourcesDir = new File(nabDir, 'sources')
String coberturaReportFormat = 'xml'

test {
  jvmArgs(
      '-Xms16m',
      '-Xmx32m',
      '-XX:PermSize=32m',
      '-XX:+UseParallelGC',
      '-Dfile.encoding=UTF-8',
      '-XX:-UseSplitVerifier',
  )

  ignoreFailures = true

  doFirst {
    // masterから本テストを実行するとmaster配下にlogディレクトリが作成されてしまったので、プロジェクト名を明記
    def logDir = new File("${projectDir}/src/test/log")
    if (!logDir.exists()) {
      assert logDir.mkdirs()
    }
  }

  beforeTest { descriptor ->
    logger.lifecycle("Running test: " + descriptor)
  }

  cobertura {
    coberturaVersion = '1.9.4.1'
    coverageSourceDirs = ["${projectDir}/src/main/java/"]
    coverageFormats = ['xml']
    coverageDirs = [project.sourceSets.main.output.classesDir]
    auxiliaryClasspath = files('src/main/resources') + configurations.testRuntime.asFileTree
  }
}

def coberturaReportDir = new File(project.buildDir, "cobertura-report")

task generateCoberturaReportByAnt(dependsOn: 'cobertura') {
  def dataFile = "${project.buildDir}/cobertura/cobertura.ser"

  inputs.file file(dataFile)
  outputs.dir coberturaReportDir

  doLast {
    ant {
      taskdef(resource: 'tasks.properties', classpath: configurations.cobertura.asPath + ':' + configurations.testRuntime.asPath)

      'cobertura-report'(
              destdir: coberturaReportDir,
              datafile: dataFile,
              srcdir: "${projectDir}/src/main/java/",
              format: 'xml'
      )
    }
  }
}

task coverage(dependsOn: ['clean', 'generateCoberturaReportByAnt'])

sonarRunner {
  sonarProperties {
    property "sonar.java.coveragePlugin", "cobertura"
    property "sonar.cobertura.reportPath", "${coberturaReportDir}/coverage.xml"
  }
}

tasks.sonarRunner {
  dependsOn = ['coverage']
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.13'
}

sourcesJar {
  into('') {
    from 'README.txt'
  }
}
